plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'com.epages.restdocs-api-spec' version '0.19.2'
}

group = 'com'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    snippetsDir = file("build/generated-snippets")
}

dependencies {

    // spring boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    // thymeleaf
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    // mysql
    runtimeOnly 'com.mysql:mysql-connector-j'
    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // test
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // 개발 도구 (자동 리로드)
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    // pk - tsid
    implementation group: 'com.github.f4b6a3', name: 'tsid-creator', version: '5.2.6'
    implementation group: 'io.hypersistence', name: 'hypersistence-utils-hibernate-63', version: '3.10.3'
    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'com.h2database:h2'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // spring rest docs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // REST Docs → OpenAPI 스펙 자동 생성
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.2'
}

tasks.named('test') {
    useJUnitPlatform()
    outputs.dir snippetsDir

    // 테스트 실행 전 스니펫 디렉토리 초기화
    // 기존 스니펫이 남아있어서 openapi.yaml이 이상하게 생성되는 문제 방지
    doFirst {
        delete snippetsDir
    }
}

tasks.named("asciidoctor") {
    configurations("asciidoctorExt")
    inputs.dir(snippetsDir)
    dependsOn("test")
}

tasks.register("copyDocs", Copy) {
    dependsOn(tasks.asciidoctor)
    from(tasks.asciidoctor.outputDir)
    into(layout.buildDirectory.dir("generated-docs"))
}

tasks.named("build") {
    dependsOn("copyDocs")
}

// OpenAPI 스펙 자동 생성 설정
openapi3 {
    title = 'Gathering API'
    description = 'Gathering 서비스 REST API 문서 (REST Docs 기반)'
    version = '1.0.0'
    format = 'yaml'
    outputDirectory = 'build/api-spec'
    outputFileNamePrefix = 'openapi'
}

// OpenAPI 스펙을 static 폴더로 복사
tasks.register('copyOpenApiSpec', Copy) {
    dependsOn 'openapi3'
    from 'build/api-spec'
    into 'src/main/resources/static/docs'
}

build {
    dependsOn copyOpenApiSpec
}

